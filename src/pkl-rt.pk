/* pkl-rt.pkl - Run-time library for the poke compiler.  */

/* Copyright (C) 2019, 2020 Jose E. Marchesi.  */

/* This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

/* Please note, in this file:

   - No standard types.
   - Use a `_pkl_' prefix for every declaration that is not defining
     a compiler-builtin wrapper.
   - No array or struct operators, only simple values please.
   - Compiler __builtins__ can be used in this file.

   You break it, you fix it.  */

/* XXX: every declaration here should be immutable.  */

defvar true = 1;
defvar false = 0;

/* Compiler built-ins. */

defun rand = int<32>: __PKL_BUILTIN_RAND__;
defun get_endian = int<32>: __PKL_BUILTIN_GET_ENDIAN__;
defun set_endian = (int<32> endian) int<32>: __PKL_BUILTIN_SET_ENDIAN__;
defun get_ios = int<32>: __PKL_BUILTIN_GET_IOS__;
defun set_ios = (int<32> ios) int<32>: __PKL_BUILTIN_SET_IOS__;
defun open = (string handler, uint<64> flags = 0) int<32>: __PKL_BUILTIN_OPEN__;
defun close = (int<32> ios) void: __PKL_BUILTIN_CLOSE__;
defun iosize = (int<32> ios) offset<uint<64>,1>: __PKL_BUILTIN_IOSIZE__;

defvar ENDIAN_LITTLE = 0;
defvar ENDIAN_BIG = 1;

/* IOS flags and modes.

   There is space for 64 flags in the uint<64> optional argument to
   `open'.  The flags in the least-significative 32 bits are valid for
   all IOD backends and are defined below.  The most-significative 32
   bits are reserved for backend-specific flags.

   Please keep these values in sync with the constants in ios.c.  */

defvar IOS_F_READ   = 1;
defvar IOS_F_WRITE  = 2;
defvar IOS_F_TRUNCATE = 8;
defvar IOS_F_CREATE = 16;

defvar IOS_M_RDONLY = IOS_F_READ;
defvar IOS_M_WRONLY = IOS_F_WRITE;
defvar IOS_M_RDWR = IOS_F_READ | IOS_F_WRITE;

/* Exceptions.

   XXX: use an enumeration?

   User-defined exceptions start with 255.  */

deftype Exception = int<32>;

defvar E_generic       = 0;
defvar E_div_by_zero   = 1;
defvar E_no_ios        = 2;
defvar E_no_return     = 3;
defvar E_out_of_bounds = 4;
defvar E_map_bounds    = 5;
defvar E_eof           = 6;
defvar E_map           = 7;
defvar E_conv          = 8;
defvar E_elem          = 9;
defvar E_constraint    = 10;
defvar E_io            = 11;
defvar E_signal        = 12;

/* Default exception handler.

   Note that the code in this function should NOT raise any exception,
   or be ready to underflow the exception handlers stack and face some
   ugly shit.  You have been warned!  */

defun _pkl_exception_handler = (Exception exception) void:
  {
   if (exception == E_signal)
     return;

   print "unhandled ";

   /* XXX: use a multiple conditional statement here.  */
   if (exception == E_out_of_bounds)
     print "out of bounds";
   else if (exception == E_eof)
     print "EOF";
   else if (exception == E_elem)
     print "invalid element";
   else if (exception == E_constraint)
     print "constraint violation";
   else if (exception == E_conv)
     print "conversion error";
   else if (exception == E_map_bounds)
     print "out of map bounds";
   else if (exception == E_map)
     print "no map";
   else if (exception == E_div_by_zero)
     print "division by zero";
   else if (exception == E_no_ios)
     print "no IOS";
   else if (exception == E_no_return)
     print "no return";
   else if (exception == E_io)
     print "generic IO";
   else
     print "generic";

   print " exception\n";
  }

/* Find the greatest common divisor of two unsigned 64-bit integrals A
   and B using the Euclidean algorithm.  */

defun _pkl_gcd = (uint<64> a, uint<64> b) uint<64>:
  {
   if (b == 0)
     return a;
   else
     return _pkl_gcd (b, a % b);
  }

/* XXX Array to string.
   Struct to string.  */
/* XXX error reporting routine, called by the macro pkl_asm_rterror().
   This should exit the vm with the exitvm instruction.  */
