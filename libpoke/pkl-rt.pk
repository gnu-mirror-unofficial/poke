/* pkl-rt.pk - Run-time library for the poke compiler.  */

/* Copyright (C) 2019, 2020, 2021, 2022 Jose E. Marchesi.  */

/* This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

/* Compiler built-ins. */

fun rand = (uint<32> seed = 0) int<32>: __PKL_BUILTIN_RAND__;
fun get_endian = int<32>: __PKL_BUILTIN_GET_ENDIAN__;
fun set_endian = (int<32> endian) int<32>: __PKL_BUILTIN_SET_ENDIAN__;
fun get_ios = int<32>: __PKL_BUILTIN_GET_IOS__;
fun set_ios = (int<32> ios) int<32>: __PKL_BUILTIN_SET_IOS__;
fun open = (string handler, uint<64> flags = 0) int<32>: __PKL_BUILTIN_OPEN__;
fun close = (int<32> ios) void: __PKL_BUILTIN_CLOSE__;
fun iosize = (int<32> ios = get_ios) offset<uint<64>,8>: __PKL_BUILTIN_IOSIZE__;
fun ioflags = (int<32> ios = get_ios) uint<64>: __PKL_BUILTIN_IOFLAGS__;
fun iobias = (int<32> ios = get_ios) offset<uint<64>,1>: __PKL_BUILTIN_IOGETB__;
fun iosetbias = (offset<uint<64>,1> bias = 0#1, int<32> ios = get_ios) void: __PKL_BUILTIN_IOSETB__;
fun getenv = (string name) string: __PKL_BUILTIN_GETENV__;
fun flush = (int<32> ios, offset<uint<64>,1> offset) void: __PKL_BUILTIN_FORGET__;
fun get_time = int<64>[2]: __PKL_BUILTIN_GET_TIME__;
fun sleep = (int<64> sec, int<64> nsec = 0) void: __PKL_BUILTIN_SLEEP__;
fun strace = void: __PKL_BUILTIN_STRACE__;
fun term_get_color = int<32>[3]: __PKL_BUILTIN_TERM_GET_COLOR__;
fun term_set_color = (int<32>[3] color) void: __PKL_BUILTIN_TERM_SET_COLOR__;
fun term_get_bgcolor = int<32>[3]: __PKL_BUILTIN_TERM_GET_BGCOLOR__;
fun term_set_bgcolor = (int<32>[3] color) void: __PKL_BUILTIN_TERM_SET_BGCOLOR__;
fun term_begin_class = (string class) void: __PKL_BUILTIN_TERM_BEGIN_CLASS__;
fun term_end_class = (string class) void: __PKL_BUILTIN_TERM_END_CLASS__;
fun term_begin_hyperlink = (string url, string id = "") void: __PKL_BUILTIN_TERM_BEGIN_HYPERLINK__;
fun term_end_hyperlink = void: __PKL_BUILTIN_TERM_END_HYPERLINK__;

fun vm_obase = int<32>: __PKL_BUILTIN_VM_OBASE__;
fun vm_set_obase = (int<32> obase) void: __PKL_BUILTIN_VM_SET_OBASE__;
fun vm_opprint = int<32>: __PKL_BUILTIN_VM_OPPRINT__;
fun vm_set_opprint = (int<32> pprint_p) void: __PKL_BUILTIN_VM_SET_OPPRINT__;
fun vm_oacutoff = int<32>: __PKL_BUILTIN_VM_OACUTOFF__;
fun vm_set_oacutoff = (int<32> oacutoff) void: __PKL_BUILTIN_VM_SET_OACUTOFF__;
fun vm_odepth = int<32>: __PKL_BUILTIN_VM_ODEPTH__;
fun vm_set_odepth = (int<32> odepth) void: __PKL_BUILTIN_VM_SET_ODEPTH__;
fun vm_oindent = int<32>: __PKL_BUILTIN_VM_OINDENT__;
fun vm_set_oindent = (int<32> oindent) void: __PKL_BUILTIN_VM_SET_OINDENT__;
fun vm_omaps = int<32>: __PKL_BUILTIN_VM_OMAPS__;
fun vm_set_omaps = (int<32> omaps) void: __PKL_BUILTIN_VM_SET_OMAPS__;
fun vm_omode = int<32>: __PKL_BUILTIN_VM_OMODE__;
fun vm_set_omode = (int<32> omode) void: __PKL_BUILTIN_VM_SET_OMODE__;

fun __pkl_unsafe_string_set = (string dst, uint<64> index, string str) void:
  __PKL_BUILTIN_UNSAFE_STRING_SET__;

var ENDIAN_LITTLE = 0;
var ENDIAN_BIG = 1;

/* Please keep the following two variables in sync with enum pvm_omode
   in pvm.h.  */
var VM_OMODE_PLAIN = 0;
var VM_OMODE_TREE = 1;

/* IOS flags and modes.

   There is space for 64 flags in the uint<64> optional argument to
   `open'.  The flags in the least-significative 32 bits are valid for
   all IOD backends and are defined below.  The most-significative 32
   bits are reserved for backend-specific flags.

   Please keep these values in sync with the constants in ios.h.  */

var IOS_F_READ   = 1;
var IOS_F_WRITE  = 2;
var IOS_F_CREATE = 16;

var IOS_M_RDONLY = IOS_F_READ;
var IOS_M_WRONLY = IOS_F_WRITE;
var IOS_M_RDWR = IOS_F_READ | IOS_F_WRITE;

/* Exceptions.  */

/* IMPORTANT: if you make changes to the Exception struct, please
   update the pvm_make_exception function in pvm-val.c
   accordingly.  */

type Exception =
  struct
  {
    int<32> code;
    string name;
    int<32> exit_status;
    string location;
    string msg;
  };

/* Standard exception codes.
   These codes should be in sync with PVM_E_* macros in pvm.h and
   PK_EC_* macros libpoke.h.
   Note that user-defined exceptions must have codes starting with
   255 (and should be registered using exception_code function defined
   below).
   Note also that EC_generic _must_ be zero.  */

var EC_generic       = 0;
var EC_div_by_zero   = 1;
var EC_no_ios        = 2;
var EC_no_return     = 3;
var EC_out_of_bounds = 4;
var EC_map_bounds    = 5;
var EC_eof           = 6;
var EC_map           = 7;
var EC_conv          = 8;
var EC_elem          = 9;
var EC_constraint    = 10;
var EC_io            = 11;
var EC_signal        = 12;
var EC_io_flags      = 13;
var EC_inval         = 14;
var EC_exit          = 15;
var EC_assert        = 16;
var EC_overflow      = 17;
var EC_perm          = 18;

/* Standard exceptions.  */

var E_generic
  = Exception {code = EC_generic, name = "generic", exit_status = 1};
var E_div_by_zero
  = Exception {code = EC_div_by_zero, name = "division by zero", exit_status = 1};
var E_no_ios
  = Exception {code = EC_no_ios, name = "no IOS", exit_status = 1};
var E_no_return
  = Exception {code = EC_no_return, name = "no return", exit_status = 1};
var E_out_of_bounds
  = Exception {code = EC_out_of_bounds, name = "out of bounds", exit_status = 1};
var E_map_bounds
  = Exception {code = EC_map_bounds, name = "out of map bounds", exit_status = 1};
var E_eof
  = Exception {code = EC_eof, name = "EOF", exit_status = 0 };
var E_map
  = Exception {code = EC_map, name = "no map", exit_status = 1};
var E_conv
  = Exception {code = EC_conv, name = "conversion error", exit_status = 1};
var E_elem
  = Exception {code = EC_elem, name = "invalid element", exit_status = 1};
var E_constraint
  = Exception {code = EC_constraint, name = "constraint violation", exit_status = 1};
var E_io
  = Exception {code = EC_io, name = "generic IO", exit_status = 1};
var E_signal
  = Exception {code = EC_signal, exit_status = 1};
var E_io_flags
  = Exception {code = EC_io_flags, name = "invalid IO flags", exit_status = 1};
var E_inval
  = Exception {code = EC_inval, name = "invalid argument", exit_status = 1};
var E_exit
  = Exception {code = EC_exit, name = "exit", exit_status = 0};
var E_assert
  = Exception {code = EC_assert, name = "assertion failure", exit_status = 1};
var E_overflow
  = Exception {code = EC_overflow, name = "overflow", exit_status = 1};
var E_perm
  = Exception {code = EC_perm, name = "wrong permissions", exit_status = 1};

/* Registration of user-defined exceptions */

type _ExceptionCodeGenerator = () int<32>;
var exception_code = lambda _ExceptionCodeGenerator:
  {
    var n = 255; /* First available code for user-defined exceptions */

    return lambda int<32>: { return n++; };
  }();

/* Find the greatest common divisor of two unsigned 64-bit integrals A
   and B using the Euclidean algorithm.  */

fun _pkl_gcd = (uint<64> a, uint<64> b) uint<64>:
  {
   if (b == 0)
     return a;
   else
     return _pkl_gcd (b, a % b);
  }

/* Return the prefix to use when printing integral magnitudes of the
   given numerical base.  This may return an empty prefix.  */

fun _pkl_base_prefix = (int<32> base) string:
 {
   if (base == 2)
      return "0b";
   if (base == 8)
      return "0o";
   if (base == 16)
      return "0x";

   return "";
 }

/* Concatenate three strings.  */

fun _pkl_strcat3 = (string a, string b, string c) string:
  {
    var la = a'length,
        lab = la + b'length,
        labc = lab + c'length,
        s = "?" * labc;

    __pkl_unsafe_string_set (s, 0, a);
    __pkl_unsafe_string_set (s, la, b);
    __pkl_unsafe_string_set (s, lab, c);
    return s;
  }

/* Given a DEPTH, indentation step ISTEP and OMODE, returns a string for
   indentation.  */

fun _pkl_indentation = (int<32> depth, int<32> istep, int<32> omode) string:
  {
    if (omode != VM_OMODE_TREE)
      return "";

    var s = " " * (1 + istep * depth);

    __pkl_unsafe_string_set (s, 0, "\n");
    return s;
  }

/* Return the suffix corresponding to the integral type with signedness
   SIGNED_P and size SIZE.  */

fun _pkl_format_int_suffix = (int<32> signed_p, uint<64> size) string:
  {
    return (signed_p ? "" : "U") +
           (size == 4 ? "N" :
            size == 8 ? "B" :
            size == 16 ? "H" :
            size == 64 ? "L" : "");
  }

/* Reduces an array of string to a single string by concatenting the
   NELEM elements of ARR starting from specified INDEX.  */

fun _pkl_reduce_string_array = (string[] arr,
                                uint<64> index, uint<64> nelem) string:
  {
    var slen = 0UL,
        first = index,
        last = index + nelem;

    while (first < last)
      {
        slen += arr[first]'length;
        ++first;
      }

    var s = " " * slen;  /* Allocate the final string once.  */

    first = index;
    for (var cursor = 0UL; first < last; ++first)
      {
        __pkl_unsafe_string_set (s, cursor, arr[first]);
        cursor += arr[first]'length;
      }
    return s;
  }

/* Return a new string in which all the non-printable characters are replaced
   with a escape sequence (\xXX).  */

fun _pkl_escape_string = (string str) string:
  {
    var chars = "0123456789abcdef",
        len = str'length,
        len_esc = 0;

    /* Calculate the length of escaped string.  */
    for (var i = 0UL; i < len; ++i)
      len_esc +=
        str[i] == '\t' || str[i] == '\n' || str[i] == '"' || str[i] == '\\' ? 2
          : str[i] - 0x20UB < 0x5fUB /* is printable */ ? 1
          : 4 /* \xXX */;

    var esc = "?" * len_esc;

    for (var i = 0UL, j = 0UL; i < len; ++i)
      {
        if (str[i] == '\t')
          {
            __pkl_unsafe_string_set (esc, j, "\\t");
            j += 2;
          }
        else if (str[i] == '\n')
          {
            __pkl_unsafe_string_set (esc, j, "\\n");
            j += 2;
          }
        else if (str[i] == '"')
          {
            __pkl_unsafe_string_set (esc, j, "\\\"");
            j += 2;
          }
        else if (str[i] == '\\')
          {
            __pkl_unsafe_string_set (esc, j, "\\\\");
            j += 2;
          }
        else if (str[i] - 0x20UB < 0x5fUB)
          {
            __pkl_unsafe_string_set (esc, j, str[i] as string);
            ++j;
          }
        else
          {
            var nib_lo = chars[str[i] & 0xfUB] as string,
                nib_hi = chars[(str[i] .>> 4) & 0xfUB] as string;

            __pkl_unsafe_string_set (esc, j, "\\x" + nib_hi + nib_lo);
            j += 4;
          }
      }
    return esc;
  }

/* Return a string with the name corresponding to the given unit in
   bits.  This may be an empty string for an unknown unit.

   This should go away once we have a run-time mechanism to store and
   access unit types.  */

var _pkl_unit_bit =    1UL;
var _pkl_unit_nibble = 4UL;
var _pkl_unit_byte =   8UL;

var _pkl_unit_kilobit = _pkl_unit_bit * 1000;
var _pkl_unit_kilobyte = _pkl_unit_byte * 1000;
var _pkl_unit_megabit = _pkl_unit_kilobit * 1000;
var _pkl_unit_megabyte = _pkl_unit_kilobyte * 1000;
var _pkl_unit_gigabit = _pkl_unit_megabit * 1000;
var _pkl_unit_gigabyte = _pkl_unit_megabyte * 1000;

var _pkl_unit_kibibit = _pkl_unit_bit * 1024;
var _pkl_unit_kibibyte = _pkl_unit_byte * 1024;
var _pkl_unit_mebibit = _pkl_unit_kibibit * 1024;
var _pkl_unit_mebibyte = _pkl_unit_kibibyte * 1024;
var _pkl_unit_gibibit = _pkl_unit_mebibit * 1024;
var _pkl_unit_gibibyte = _pkl_unit_mebibyte * 1024;

fun _pkl_unit_name = (uint<64> bits) string:
 {
    if (bits == _pkl_unit_bit)
       return "b";
    if (bits == _pkl_unit_nibble)
       return "N";
    if (bits == _pkl_unit_byte)
       return "B";
    if (bits == _pkl_unit_kilobit)
       return "Kb";
    if (bits == _pkl_unit_kilobyte)
       return "KB";
    if (bits == _pkl_unit_megabit)
       return "Mb";
    if (bits == _pkl_unit_megabyte)
       return "MB";
    if (bits == _pkl_unit_gigabit)
       return "Gb";
    if (bits == _pkl_unit_gigabyte)
       return "GB";
    if (bits == _pkl_unit_kibibit)
       return "Kib";
    if (bits == _pkl_unit_kibibyte)
       return "KiB";
    if (bits == _pkl_unit_mebibit)
       return "Mib";
    if (bits == _pkl_unit_mebibyte)
       return "MiB";
    if (bits == _pkl_unit_gibibit)
       return "Gib";
    if (bits == _pkl_unit_gibibyte)
       return "GiB";

    return "";
  }

/* Assertion function.

   The compiler transforms assert statement to invocation of this
   function.  COND is first argument of assert statement, and MSG is
   the optional second argument.  FILENAME is the name of source
   file.  LINE and COL are, respectively, line and column number
   of the assert statement in the source file.  */

fun _pkl_assert = (uint<64> cond, string msg, string filename,
                   uint<64> line, uint<64> col) void:
  {
    if (cond)
      return;

    raise Exception {
      code = EC_assert,
      name = "assertion failure",
      location = format ("%s:%u64d:%u64d:", filename, line, col),
      msg = msg,
      exit_status = 1,
    };
  }

/* The Type struct below describes the values returned by the `typeof'
   language construction.  This type is used in the compiler phases,
   so pretty please do not use `typeof' until the compiler has been
   bootstrapped.

   Fun facts:
   1) If you do changes to the struct Pk_Type defined below it is most
      likely you will have to adapt the typifier RAS functions/macros
      accordingly.
   2) The PK_TYPE_* values _must_ resolve to a constant integral value
      at compile-time.  Constant folding is ok.  Changing their values
      is ok.
*/

var PK_TYPE_UNKNOWN =  0;
var PK_TYPE_INTEGRAL = 1;
var PK_TYPE_OFFSET   = 2;
var PK_TYPE_STRING   = 3;
var PK_TYPE_ARRAY    = 4;
var PK_TYPE_STRUCT   = 5;
var PK_TYPE_FUNCTION = 6;
var PK_TYPE_ANY      = 7;

type Pk_Type =
  struct
  {
    uint<32> code;
    string name;
    uint<32> complete_p;
    int<32> signed_p    if code in [PK_TYPE_INTEGRAL, PK_TYPE_OFFSET, PK_TYPE_STRUCT];
    uint<64> size       if code in [PK_TYPE_INTEGRAL, PK_TYPE_OFFSET, PK_TYPE_STRUCT];
    uint<64> _unit      if code == PK_TYPE_OFFSET;
    int<32> bounded_p   if code == PK_TYPE_ARRAY;
    int<32> union_p     if code == PK_TYPE_STRUCT;
    int<32> pinned_p    if code == PK_TYPE_STRUCT;
    int<32> integral_p  if code == PK_TYPE_STRUCT;
    int<32> nfields     if code == PK_TYPE_STRUCT;
    int<32> nmethods    if code == PK_TYPE_STRUCT;
    string[] fnames     if code == PK_TYPE_STRUCT;
    string[] ftypes     if code == PK_TYPE_STRUCT;
    string[] mnames     if code == PK_TYPE_STRUCT;
    string[] mtypes     if code == PK_TYPE_STRUCT;

    type Mapper_Fn = (int<32>,int<32>,uint<64>,uint<64>,uint<64>)any;

    Mapper_Fn mapper             if code in [PK_TYPE_ARRAY, PK_TYPE_STRUCT];
    (any)void writer             if code in [PK_TYPE_ARRAY, PK_TYPE_STRUCT];
    (any,int<32>)string formater if code in [PK_TYPE_ARRAY, PK_TYPE_STRUCT];
    (any)any printer             if code in [PK_TYPE_ARRAY, PK_TYPE_STRUCT];
    (any)any integrator          if code in [PK_TYPE_ARRAY, PK_TYPE_STRUCT];
    (any)any deintegrator        if code == PK_TYPE_STRUCT;
    (any,any)int<32> comparator  if code == PK_TYPE_STRUCT;
  };

/**** Set the default load path ****/

var load_path = "";

/* First, add the system-wide locations.  */

try load_path = getenv ("POKEDATADIR");
catch if E_inval { load_path = "%DATADIR%"; }

try load_path = getenv ("POKEPICKLESDIR") + ":" + load_path;
catch if E_inval { load_path = "%DATADIR%/pickles" + ":" + load_path; }

/* Add the directories prescribed by the XDG Base Directory
   Specification as described in
   https://specifications.freedesktop.org/basedir-spec/basedir-spec-latest.html */

try load_path = getenv ("XDG_DATA_HOME") + ":" + load_path;
catch if E_inval {
  try load_path = getenv ("HOME") + "/.local/share/poke:" + load_path;
  catch if E_inval {}
}

try load_path = getenv ("XDG_DATA_DIRS") + ":" + load_path;
catch if E_inval { }

/* Add the directories in POKE_LOAD_PATH, if defined.  */
try load_path = getenv ("POKE_LOAD_PATH") + ":" + load_path;
catch if E_inval { }

/* Add the current working directory.  */
load_path = ".:" + load_path;
